Bahurum

high

# Unexpected liquidations after batchSwap with intermediate amounts

## Summary
When calling Balancer `batchSwap` with an account, one can specify in the input data the intermediate amount in or out of each swap. If the amount-in of a swap is less than the amount-out of the previous swap, then the difference will be sent to the account. `BalancerController` does not consider this and only adds to the collateral the token received from the last swap, so the total collateral value for the account will be underestimated, leading to lower margin and unexpected liquidation.

## Vulnerability Detail
The struct `BatchSwapStep` holds the information of a swap of the chain of swaps.
``` solidity
struct BatchSwapStep {
    bytes32 poolId;
    uint256 assetInIndex;
    uint256 assetOutIndex;
    uint256 amount;
    bytes userData;
}
```

Let's assume `kind` = 0, meaning that `amount` is the amount of token in swapped for the swap. 
If `amount` of the i-th swap is less than the amount received from the (i-1)-th swap, then the difference will be sent to the account. This allows users to swap a token for multiple tokens.
Note that leaving `amount` = 0 will swap all the tokens received from the previous swap.
1. Owner creates account and deposits 4000 DAI
2. Account borrows 2000 DAI
3. Account swaps with `batchSwap` 6000 DAI with an equal split of USDC and token ABC, with the following order DAI --> USDC --> ABC. This is done by specifying `swaps[1].amount`. 
   
    3.1. Account receives 3000 USDC and 3000 USD worth of ABC token. 
    
    3.2 USDC is not added in `tokensIn`, so it is not added as a collateral in `_updateTokensIn(account, tokensIn)`
4. ABC token price falls by 20 %. The health factor of the account is 3000*(1-0.2)/2000 = 1.2. The health factor, considering USDC in the account, should be (3000*(1-0.2) + 3000)/2000 = 2.7
5. Account is liquidated unexpectedly and owner of the account looses 400 USD (liquidation premium)

Note that the 3000 USDC can still be recovered by the account owner by calling `AccountManager.withdraw(account, addressUSDC, 3000)`
## Impact
Health factor calculated is lower than it should be, leading to early liquidation and losses for the account owner

## Code Snippet

```solidity
    function canBatchSwap(address, bool, bytes calldata data)
        internal
        pure
        returns (bool, address[] memory, address[] memory)
    {
        (
            ,
            IVault.BatchSwapStep[] memory swaps,
            IAsset[] memory assets,
            ,
            ,
        ) = abi.decode(data, (
                uint8, IVault.BatchSwapStep[], IAsset[], IVault.FundManagement, uint256[], uint256
            )
        );

        if (!isMultiHopSwap(swaps))
            return (false, new address[](0), new address[](0));

        uint tokenInIndex = swaps[swaps.length - 1].assetOutIndex;
        uint tokenOutIndex = swaps[0].assetInIndex;

        address[] memory tokensIn;
        address[] memory tokensOut;

        if (address(assets[tokenOutIndex]) == address(0)) {
            tokensIn = new address[](1);
            tokensIn[0] = address(assets[tokenInIndex]);
            return (
                true,
                tokensIn,
                new address[](0)
            );
        }

        if (address(assets[tokenInIndex]) == address(0)) {
            tokensOut = new address[](1);
            tokensOut[0] = address(assets[tokenOutIndex]);
            return (
                true,
                new address[](0),
                tokensOut
            );
        }

        tokensIn = new address[](1);
        tokensOut = new address[](1);
        tokensOut[0] = address(assets[tokenOutIndex]);
        tokensIn[0] = address(assets[tokenInIndex]);

        return (
            true,
            tokensIn,
            tokensOut
        );
    }
```
## Tool used

Manual Review

## Recommendation
A possible fix is to prevent the account from receiving the intermediate `assets`, by enforcing `amount` to be 0. This could be done in `isMultiHopSwap`


```solidity
    function isMultiHopSwap(IVault.BatchSwapStep[] memory swaps)
        internal
        pure
        returns (bool)
    {
        uint steps = swaps.length;
        for (uint i; i < steps - 1; i++) {
            if (swaps[i].assetOutIndex != swaps[i+1].assetInIndex)
                return false;
+           if (swaps[i+1].amount > 0)
+               return false
        }
        return true;
    }
```